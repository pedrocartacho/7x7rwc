// ==============================================================================
// Arenas 7vs7 modificada por rodrigo aka narutekz (2014), Fissuris (2017) e Moskaum Y (2024)
// ==============================================================================
quiz_02,130,343,5	script	Waiting Room::kvm50R_Guillaume	418,{
	end;
	
OnInit:
	while (1){
		sleep 2000;
		if (getwaitingroomstate (1) != $@jogadoresbluechat){
			delwaitingroom;
			waitingroom "Arena 20 - 1@lab",$@jogadoresbluechat,"kvm50_BG::OnGuillaumeJoin",1;
			}
	}
	end;
	
OnEnterBG:
	set $@kvm50BG_id1, waitingroom2bg("izlude",128,105,"kvm50_BG::OnGuillaumeQuit","kvm50_BG::OnGuillaumeDie");
	end;
}

quiz_02,165,343,4	script	Waiting Room::kvm50R_Croix	414,{
	end;

OnInit:
	while (1){
		sleep 2000;
		if (getwaitingroomstate (1) != $@jogadoresredchat){
			delwaitingroom;
			waitingroom "Arena 20 - 1@lab",$@jogadoresredchat,"kvm50_BG::OnCroixJoin",1;
			}
	}
	end;

OnEnterBG:
	set $@kvm50BG_id2, waitingroom2bg("izlude",128,105,"kvm50_BG::OnCroixQuit","kvm50_BG::OnCroixDie");
	end;
}

// Battleground Engine
// *********************************************************************

-	script	kvm50_BG	-1,{
	end;

OnGuillaumeJoin:
OnCroixJoin:
	donpcevent "kvm50_BG::OnReadyCheck";
	end;

OnGuillaumeQuit:
	set BG_Delay_Tick, gettimetick(2) + 2000;
OnGuillaumeDie:
	if( $@kvm50BG == 2 )
	{
		set .Guillaume_Count, .Guillaume_Count - 1;
		set .Croix_Score, .Croix_Score + 1;
		bg_updatescore "1@lab",.Guillaume_Score,.Croix_Score;
		if( .Guillaume_Count < 1 ){
			donpcevent "kvm50_BG::OnCroixWin";
		}
	}
	end;

OnCroixQuit:
	set BG_Delay_Tick, gettimetick(2) + 2000;
OnCroixDie:
	if( $@kvm50BG == 2 )
	{
		set .Croix_Count, .Croix_Count - 1;
		set .Guillaume_Score, .Guillaume_Score + 1;
		bg_updatescore "1@lab",.Guillaume_Score,.Croix_Score;
		if( .Croix_Count < 1 ){
			donpcevent "kvm50_BG::OnGuillaumeWin";
		}
	}
	end;

OnReadyCheck:
	if( $@kvm50BG )
		end;
	set .@Guillaume, getwaitingroomstate(0,"kvm50R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"kvm50R_Croix");

	if((.@Guillaume < $@jogadoresblue) || (.@Croix < $@jogadoresred))
	{
		if( .@Guillaume > 1 && .@Croix > 1 && !agitcheck() && $@KvM_Flood < gettimetick(2) )
		{
			announce "Arena 20 - 1@lab | Blue Team: " + .@Guillaume + "/"+$@jogadoresblue+", Red Team: " + .@Croix + "/"+$@jogadoresred+"",0,0x00bfff;
			set $@KvM_Flood, gettimetick(2) + 15;
		}
		else
			mapannounce "quiz_02","Arena 20 - 1@lab | Blue Team: " + .@Guillaume + "/"+$@jogadoresblue+", Red Team: " + .@Croix + "/"+$@jogadoresred+"",1,0x00bfff;
		end;
	}

	if ((.@Guillaume == 0) || (.@Croix == 0)) {
		end;
	}
	
	set $@kvm50BG, 1; // Starting
	donpcevent "kvm50R_Croix::OnEnterBG";
	donpcevent "kvm50R_Guillaume::OnEnterBG";
	donpcevent "kvm50_BG::OnStart";
	end;

OnStart:
	announce "- Duas equipes entraram na [ Arena 20 - 1@lab ]. -", bc_yellow;
	set $@kvm50BG_Victory, 0;
	// Warp Teams
	bg_warp $@kvm50BG_id1,"1@lab",9,50;
	bg_warp $@kvm50BG_id2,"1@lab",91,50;

	if ($@guildsInArena2 > 0) {
        $@guildsInArena2--;
    }
	if ($@guildsInArena2 > 0) {
        $@guildsInArena2--;
    }

	setwall "1@lab",17,53,8,4,0,"batc20wall_a";
	setwall "1@lab",16,52,8,4,0,"batc20wall_b";
	setwall "1@lab",85,52,8,4,0,"batc20wall_c";
	setwall "1@lab",84,53,8,4,0,"batc20wall_d";

	sleep 100;
	// ScoreBoard
	
	set .Guillaume_Score, 0;
	set .Croix_Score, 0;
	initnpctimer;
	end;

OnTimer3000:
	mapannounce "1@lab","- The match will begin in 15 seconds! -", bc_yellow;
	end;

OnTimer15000:
	mapannounce "1@lab","- The match will begin in 5 seconds! -", bc_yellow;
	end;

OnTimer16000:
	mapannounce "1@lab","- The match will begin in 4 seconds! -", bc_yellow;
	end;

OnTimer17000:
	mapannounce "1@lab","- The match will begin in 3 seconds! -", bc_yellow;
	end;

OnTimer18000:
	mapannounce "1@lab","- The match will begin in 2 seconds! -", bc_yellow;
	end;
	
OnTimer19000:
	mapannounce "1@lab","- The match will begin in 1 second! -", bc_yellow;
	end;

OnTimer20000:
	// Team Members
	set .Guillaume_Count, bg_get_data($@kvm50BG_id1, 0);
	set .Croix_Count, bg_get_data($@kvm50BG_id2, 0);
	if( (.Guillaume_Count < $@jogadoresblue) || (.Croix_Count < $@jogadoresred) )
	{
		set $@kvm50BG_Victory, 3;
		set $@kvm50BG, 3;
		mapannounce "1@lab","- There are not enough players to start the match. -", bc_yellow;
		stopnpctimer;
		sleep 2000;
		donpcevent "kvm50_BG::OnStop";
		end;
	}

	set $@kvm50BG, 2; // Playing
	delwall "batc20wall_a";
	delwall "batc20wall_b";
	delwall "batc20wall_c";
	delwall "batc20wall_d";
	end;

OnTimer25001:
	mapannounce "1@lab","- The match started! Good luck to all. -", bc_yellow;
killmonsterall "1@lab";
	end;

OnTimer420000:
	mapannounce "1@lab","- The match will end in 30 seconds! -", bc_yellow;
	end;

OnTimer450000:
	if( .Croix_Count > .Guillaume_Count )
		donpcevent "kvm50_BG::OnCroixWin";
	else if( .Croix_Count < .Guillaume_Count )
		donpcevent "kvm50_BG::OnGuillaumeWin";
	else
	{ // Draw Game
		set $@kvm50BG, 3;
		set $@kvm50BG_Victory, 3;

		stopnpctimer;
		sleep 2000;
		announce "- As duas equipes empataram! -", bc_yellow;
		donpcevent "kvm50_BG::OnStop";
	}
	end;

OnGuillaumeWin:
	set $@kvm50BG, 3;
	set $@kvm50BG_Victory, 1;

	stopnpctimer;
	sleep 1000;	
	announce "- Blue Team VENCEU! -",bc_all,0x00bfff;
	sleep 3000;
	donpcevent "kvm50_BG::OnStop";
	end;

OnCroixWin:
	set $@kvm50BG, 3;
	set $@kvm50BG_Victory, 2;

	stopnpctimer;
	sleep 1000;
	announce "- Red Team VENCEU! -",bc_all,0xff6347;
	sleep 3000;
	donpcevent "kvm50_BG::OnStop";
	end;

OnStop:
	if ($spectator_count10 > 0) {
       set $spectator_count10, $spectator_count10 - 1;
    }
	if ($spectator_count10 > 0) {
       set $spectator_count10, $spectator_count10 - 1;
    }
	// Warp Teams
	mapwarp "1@lab","izlude",128,105;
	donpcevent "kvm50_BG_Out::OnBegin";
	end;

OnReset:
	stopnpctimer;
	stopnpctimer "kvm50_BG_Out";
	set .Croix_Count, 0;
	set .Guillaume_Count, 0;
	set .Croix_Score, 0;
	set .Guillaume_Score, 0;
	set $@kvm50BG_Victory, 0;
	if( $@kvm50BG_id1 ) { bg_destroy $@kvm50BG_id1; set $@kvm50BG_id1, 0; }
	if( $@kvm50BG_id2 ) { bg_destroy $@kvm50BG_id2; set $@kvm50BG_id2, 0; }
	sleep 2000;
	mapwarp "1@lab","izlude",128,105;
	sleep 2000;
	maprespawnguildid "1@lab",0,3; // Just in case someone else
	bg_updatescore "1@lab",0,0;
	sleep 2000;
	set $@kvm50BG, 0;
	donpcevent "kvm50_BG::OnReadyCheck"; // Maybe a game is ready to start
	end;
}

-	script	kvm50_BG_Out	-1,{
	end;

OnBegin:
	initnpctimer;
	end;

OnTimer50000:
	mapannounce "1@lab","- The match will end in 10 seconds! -",1,0x696969;
	end;
OnTimer3000:
	donpcevent "kvm50_BG::OnReset";
	end;
}