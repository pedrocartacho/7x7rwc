// ==============================================================================
// Arenas 7vs7 modificada por rodrigo aka narutekz (2014), Fissuris (2017) e Moskaum Y (2024)
// ==============================================================================
quiz_02,134,347,5	script	Waiting Room::KvM34R_Guillaume	418,{
	end;
	
OnInit:
	while (1){
		sleep 2000;
		if (getwaitingroomstate (1) != $@jogadoresbluechat){
			delwaitingroom;
			waitingroom "Arena 4 - guild_vs5",$@jogadoresbluechat,"KvM34_BG::OnGuillaumeJoin",1;
			}
	}
	end;
	
OnEnterBG:
	set $@KvM34BG_id1, waitingroom2bg("izlude",128,105,"KvM34_BG::OnGuillaumeQuit","KvM34_BG::OnGuillaumeDie");
	end;
}

quiz_02,161,347,4	script	Waiting Room::KvM34R_Croix	414,{
	end;

OnInit:
	while (1){
		sleep 2000;
		if (getwaitingroomstate (1) != $@jogadoresredchat){
			delwaitingroom;
			waitingroom "Arena 4 - guild_vs5",$@jogadoresredchat,"KvM34_BG::OnCroixJoin",1;
			}
	}
	end;

OnEnterBG:
	set $@KvM34BG_id2, waitingroom2bg("izlude",128,105,"KvM34_BG::OnCroixQuit","KvM34_BG::OnCroixDie");
	end;
}

// Battleground Engine
// *********************************************************************

-	script	KvM34_BG	-1,{
	end;

OnGuillaumeJoin:
OnCroixJoin:
	donpcevent "KvM34_BG::OnReadyCheck";
	end;

OnGuillaumeQuit:
	set BG_Delay_Tick, gettimetick(2) + 2000;
OnGuillaumeDie:
	if( $@KvM34BG == 2 )
	{
		set .Guillaume_Count, .Guillaume_Count - 1;
		set .Croix_Score, .Croix_Score + 1;
		bg_updatescore "guild_vs5",.Guillaume_Score,.Croix_Score;
		if( .Guillaume_Count < 1 ){
			donpcevent "KvM34_BG::OnCroixWin";
		}
	}
	end;

OnCroixQuit:
	set BG_Delay_Tick, gettimetick(2) + 2000;
OnCroixDie:
	if( $@KvM34BG == 2 )
	{
		set .Croix_Count, .Croix_Count - 1;
		set .Guillaume_Score, .Guillaume_Score + 1;
		bg_updatescore "guild_vs5",.Guillaume_Score,.Croix_Score;
		if( .Croix_Count < 1 ){
			donpcevent "KvM34_BG::OnGuillaumeWin";
		}
	}
	end;

OnReadyCheck:
	if( $@KvM34BG )
		end;
	set .@Guillaume, getwaitingroomstate(0,"KvM34R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"KvM34R_Croix");

	if((.@Guillaume < $@jogadoresblue) || (.@Croix < $@jogadoresred))
	{
		if( .@Guillaume > 3 && .@Croix > 3 && !agitcheck() && $@KvM_Flood < gettimetick(2) )
		{
			announce "Arena 4 - guild_vs5 | Blue Team: " + .@Guillaume + "/"+$@jogadoresblue+", Red Team: " + .@Croix + "/"+$@jogadoresred+"",0,0x00FF7F;
			set $@KvM_Flood, gettimetick(2) + 15;
		}
		else
			mapannounce "quiz_02","Arena 4 - guild_vs5 | Blue Team: " + .@Guillaume + "/"+$@jogadoresblue+", Red Team: " + .@Croix + "/"+$@jogadoresred+"",1,0x00FF7F;
		end;
	}
	
	if ((.@Guillaume == 0) || (.@Croix == 0)) {
		end;
	}
	
	set $@KvM34BG, 1; // Starting
	donpcevent "KvM34R_Croix::OnEnterBG";
	donpcevent "KvM34R_Guillaume::OnEnterBG";
	donpcevent "KvM34_BG::OnStart";
	end;

OnStart:
	announce "- Duas equipes entraram na [ Arena 4 - guild_vs5 ]. -", bc_yellow;
	set $@KvM34BG_Victory, 0;
	// Warp Teams
	bg_warp $@KvM34BG_id1,"guild_vs5",18,50;
	bg_warp $@KvM34BG_id2,"guild_vs5",81,49;

	if ($@guildsInArena > 0) {
        $@guildsInArena--;
    }
	if ($@guildsInArena > 0) {
        $@guildsInArena--;
    }

	setwall "guild_vs5",22,55,12,4,0,"4a";
	setwall "guild_vs5",77,55,12,4,0,"4b";

	sleep 100;
	// ScoreBoard
	set .Guillaume_Score, 0;
	set .Croix_Score, 0;
	initnpctimer;
	end;

OnTimer3000:
	mapannounce "guild_vs5","- The match will begin in 15 seconds! -", bc_yellow;
	end;

OnTimer15000:
	mapannounce "guild_vs5","- The match will begin in 5 seconds! -", bc_yellow;
	end;

OnTimer16000:
	mapannounce "guild_vs5","- The match will begin in 4 seconds! -", bc_yellow;
	end;

OnTimer17000:
	mapannounce "guild_vs5","- The match will begin in 3 seconds! -", bc_yellow;
	end;

OnTimer18000:
	mapannounce "guild_vs5","- The match will begin in 2 seconds! -", bc_yellow;
	end;
	
OnTimer19000:
	mapannounce "guild_vs5","- The match will begin in 1 second! -", bc_yellow;
	end;

OnTimer20000:
	// Team Members
	set .Guillaume_Count, bg_get_data($@KvM34BG_id1, 0);
	set .Croix_Count, bg_get_data($@KvM34BG_id2, 0);
	if( (.Guillaume_Count < $@jogadoresblue) || (.Croix_Count < $@jogadoresred) )
	{
		set $@KvM34BG_Victory, 3;
		set $@KvM34BG, 3;
		mapannounce "guild_vs5","- There are not enough players to start the match. -", bc_yellow;
		stopnpctimer;
		sleep 2000;
		donpcevent "KvM34_BG::OnStop";
		end;
	}

	set $@KvM34BG, 2; // Playing
delwall "4a";
delwall "4b";
	end;

OnTimer25001:
	mapannounce "guild_vs5","- The match started! Good luck to all. -", bc_yellow;
killmonsterall "guild_vs5";
	end;

OnTimer420000:
	mapannounce "guild_vs5","- The match will end in 30 seconds! -", bc_yellow;
	end;

OnTimer450000:
	if( .Croix_Count > .Guillaume_Count )
		donpcevent "KvM34_BG::OnCroixWin";
	else if( .Croix_Count < .Guillaume_Count )
		donpcevent "KvM34_BG::OnGuillaumeWin";
	else
	{ // Draw Game
		set $@KvM34BG, 3;
		set $@KvM34BG_Victory, 3;

		stopnpctimer;
		sleep 2000;
		announce "- As duas equipes empataram! -", bc_yellow;
		donpcevent "KvM34_BG::OnStop";
	}
	end;

OnGuillaumeWin:
	set $@KvM34BG, 3;
	set $@KvM34BG_Victory, 1;

	stopnpctimer;
	sleep 1000;	
	announce "- Blue Team VENCEU! -",bc_all,0x00bfff;
	sleep 3000;
	donpcevent "KvM34_BG::OnStop";
	end;

OnCroixWin:
	set $@KvM34BG, 3;
	set $@KvM34BG_Victory, 2;

	stopnpctimer;
	sleep 1000;
	announce "- Red Team VENCEU! -",bc_all,0xff6347;
	sleep 3000;
	donpcevent "KvM34_BG::OnStop";
	end;

OnStop:
	if ($spectator_count4 > 0) {
       set $spectator_count4, $spectator_count4 - 1;
    }
	if ($spectator_count4 > 0) {
       set $spectator_count4, $spectator_count4 - 1;
    }
	// Warp Teams
	mapwarp "guild_vs5","izlude",128,105;
	donpcevent "KvM34_BG_Out::OnBegin";
	end;

OnReset:
	stopnpctimer;
	stopnpctimer "KvM34_BG_Out";
	set .Croix_Count, 0;
	set .Guillaume_Count, 0;
	set .Croix_Score, 0;
	set .Guillaume_Score, 0;
	set $@KvM34BG_Victory, 0;
	if( $@KvM34BG_id1 ) { bg_destroy $@KvM34BG_id1; set $@KvM34BG_id1, 0; }
	if( $@KvM34BG_id2 ) { bg_destroy $@KvM34BG_id2; set $@KvM34BG_id2, 0; }
	sleep 2000;
	mapwarp "guild_vs5","izlude",128,105;
	sleep 2000;
	maprespawnguildid "guild_vs5",0,3; // Just in case someone else
	bg_updatescore "guild_vs5",0,0;
	sleep 2000;
	set $@KvM34BG, 0;
	donpcevent "KvM34_BG::OnReadyCheck"; // Maybe a game is ready to start
	end;
}

-	script	KvM34_BG_Out	-1,{
	end;

OnBegin:
	initnpctimer;
	end;

OnTimer50000:
	mapannounce "guild_vs5","- The match will end in 10 seconds! -",1,0x696969;
	end;
OnTimer3000:
	donpcevent "KvM34_BG::OnReset";
	end;
}