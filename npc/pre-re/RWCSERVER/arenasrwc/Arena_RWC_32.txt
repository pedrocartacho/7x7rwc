// ==============================================================================
// Arenas 7vs7 modificada inicialmente por rodrigo aka narutekz (2014) e Fissuris (2017) 
// e intensamente aprimorada por Moskaum Y (2024 e 2025)
// ==============================================================================
quiz_02,129,356,5	script	Waiting Room::kvm62R_Guillaume	418,{
	//cutin "aldeg_cas02",4;
	mes "Se você tem se divertido no servidor, demonstre sua apreciação ^DB6446agora^000000 mesmo fazendo um pix em qualquer quantia que você sente ser proporcional ao entretenimento que você está tendo:";
	mes "^33940CCHAVE PIX 7x7rwc@gmail.com^000000";
	next;
	//cutin "", 255;
	end;
	
OnInit:
	while (1){
		sleep 2000;
		if (getwaitingroomstate (1) != $@jogadoresbluechat){
			delwaitingroom;
			waitingroom "Arena 32 - aldeg_cas02",$@jogadoresbluechat,"kvm62_BG::OnGuillaumeJoin",1;
			}
	}
	end;
	
OnEnterBG:
	set $@kvm62BG_id1, waitingroom2bg("izlude",128,105,"kvm62_BG::OnGuillaumeQuit","kvm62_BG::OnGuillaumeDie");
	end;
}

quiz_02,166,356,4	script	Waiting Room::kvm62R_Croix	414,{
	//cutin "arena30",4;
	mes "Se você tem se divertido no servidor, demonstre sua apreciação ^DB6446agora^000000 mesmo fazendo um pix em qualquer quantia que você sente ser proporcional ao entretenimento que você está tendo:";
	mes "^33940CCHAVE PIX 7x7rwc@gmail.com^000000";
	next;
	//cutin "", 255;
	end;

OnInit:
	while (1){
		sleep 2000;
		if (getwaitingroomstate (1) != $@jogadoresredchat){
			delwaitingroom;
			waitingroom "Arena 32 - aldeg_cas02",$@jogadoresredchat,"kvm62_BG::OnCroixJoin",1;
			}
	}
	end;

OnEnterBG:
	set $@kvm62BG_id2, waitingroom2bg("izlude",128,105,"kvm62_BG::OnCroixQuit","kvm62_BG::OnCroixDie");
	end;
}

// Battleground Engine
// *********************************************************************

-	script	kvm62_BG	-1,{
	end;

OnGuillaumeJoin:
OnCroixJoin:
	donpcevent "kvm62_BG::OnReadyCheck";
	end;

OnGuillaumeQuit:
	set BG_Delay_Tick, gettimetick(2) + 2000;
OnGuillaumeDie:
	if( $@kvm62BG == 2 )
	{
		set .Guillaume_Count, .Guillaume_Count - 1;
		set .Croix_Score, .Croix_Score + 1;
		bg_updatescore "aldeg_cas02",.Guillaume_Score,.Croix_Score;
		if( .Guillaume_Count < 1 ){
			donpcevent "kvm62_BG::OnCroixWin";
		}
	}
	end;

OnCroixQuit:
	set BG_Delay_Tick, gettimetick(2) + 2000;
OnCroixDie:
	if( $@kvm62BG == 2 )
	{
		set .Croix_Count, .Croix_Count - 1;
		set .Guillaume_Score, .Guillaume_Score + 1;
		bg_updatescore "aldeg_cas02",.Guillaume_Score,.Croix_Score;
		if( .Croix_Count < 1 ){
			donpcevent "kvm62_BG::OnGuillaumeWin";
		}
	}
	end;

OnReadyCheck:
	if( $@kvm62BG )
		end;
	set .@Guillaume, getwaitingroomstate(0,"kvm62R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"kvm62R_Croix");

	if((.@Guillaume < $@jogadoresblue) || (.@Croix < $@jogadoresred))
	{
		if( .@Guillaume > 1 && .@Croix > 1 && !agitcheck() && $@KvM_Flood < gettimetick(2) )
		{
			announce "Arena 32 - aldeg_cas02 | Blue Team: " + .@Guillaume + "/"+$@jogadoresblue+", Red Team: " + .@Croix + "/"+$@jogadoresred+"",0,0x00bfff;
			set $@KvM_Flood, gettimetick(2) + 15;
		}
		else
			mapannounce "quiz_02","Arena 32 - aldeg_cas02 | Blue Team: " + .@Guillaume + "/"+$@jogadoresblue+", Red Team: " + .@Croix + "/"+$@jogadoresred+"",1,0x00bfff;
		end;
	}

	if ((.@Guillaume == 0) || (.@Croix == 0)) {
		end;
	}
	
	set $@kvm62BG, 1; // Starting
	donpcevent "kvm62R_Croix::OnEnterBG";
	donpcevent "kvm62R_Guillaume::OnEnterBG";
	donpcevent "kvm62_BG::OnStart";
	end;

OnStart:
	announce "- Duas equipes entraram na [ Arena 32 - aldeg_cas02 ]. -", bc_yellow;
	set $@kvm62BG_Victory, 0;
	// Warp Teams
	bg_warp $@kvm62BG_id1,"aldeg_cas02",54,56;
	bg_warp $@kvm62BG_id2,"aldeg_cas02",128,61;

	if ($@guildsInArena2 > 0) {
        $@guildsInArena2--;
    }
	if ($@guildsInArena2 > 0) {
        $@guildsInArena2--;
    }
	setwall "aldeg_cas02",50,50,15,6,0,"batc20wall_a";
	setwall "aldeg_cas02",123,58,12,0,0,"batc20wall_b";
	// setwall "aldeg_cas02",379,21,9,6,0,"batc20wall_c";
	// setwall "aldeg_cas02",378,20,9,4,0,"batc20wall_d";
	// setwall "aldeg_cas02",94,42,9,6,0,"batc20wall_e";
	// setwall "aldeg_cas02",102,41,9,4,0,"batc20wall_f";

	sleep 100;
	// ScoreBoard
	
	set .Guillaume_Score, 0;
	set .Croix_Score, 0;
	initnpctimer;
	end;

OnTimer3000:
	mapannounce "aldeg_cas02","- The match will begin in 15 seconds! -", bc_yellow;
	end;

OnTimer15000:
	mapannounce "aldeg_cas02","- The match will begin in 5 seconds! -", bc_yellow;
	end;

OnTimer16000:
	mapannounce "aldeg_cas02","- The match will begin in 4 seconds! -", bc_yellow;
	end;

OnTimer17000:
	mapannounce "aldeg_cas02","- The match will begin in 3 seconds! -", bc_yellow;
	end;

OnTimer18000:
	mapannounce "aldeg_cas02","- The match will begin in 2 seconds! -", bc_yellow;
	end;
	
OnTimer19000:
	mapannounce "aldeg_cas02","- The match will begin in 1 second! -", bc_yellow;
	end;

OnTimer20000:
	// Team Members
	set .Guillaume_Count, bg_get_data($@kvm62BG_id1, 0);
	set .Croix_Count, bg_get_data($@kvm62BG_id2, 0);
	if( (.Guillaume_Count < $@jogadoresblue) || (.Croix_Count < $@jogadoresred) )
	{
		set $@kvm62BG_Victory, 3;
		set $@kvm62BG, 3;
		mapannounce "aldeg_cas02","- There are not enough players to start the match. -", bc_yellow;
		stopnpctimer;
		sleep 2000;
		donpcevent "kvm62_BG::OnStop";
		end;
	}

	set $@kvm62BG, 2; // Playing
	delwall "batc20wall_a";
	delwall "batc20wall_b";
	// delwall "batc20wall_c";
	// delwall "batc20wall_d";
	// delwall "batc20wall_e";
	// delwall "batc20wall_f";
	end;

OnTimer25001:
	mapannounce "aldeg_cas02","- The match started! Good luck to all. -", bc_yellow;
killmonsterall "aldeg_cas02";
	end;

OnTimer420000:
	mapannounce "aldeg_cas02","- The match will end in 30 seconds! -", bc_yellow;
	end;

OnTimer450000:
	if( .Croix_Count > .Guillaume_Count )
		donpcevent "kvm62_BG::OnCroixWin";
	else if( .Croix_Count < .Guillaume_Count )
		donpcevent "kvm62_BG::OnGuillaumeWin";
	else
	{ // Draw Game
		set $@kvm62BG, 3;
		set $@kvm62BG_Victory, 3;

		stopnpctimer;
		sleep 2000;
		announce "- As duas equipes empataram! -", bc_yellow;
		donpcevent "kvm62_BG::OnStop";
	}
	end;

OnGuillaumeWin:
	set $@kvm62BG, 3;
	set $@kvm62BG_Victory, 1;

	stopnpctimer;
	sleep 1000;	
	announce "- Blue Team VENCEU! -",bc_all,0x00bfff;
	sleep 3000;
	donpcevent "kvm62_BG::OnStop";
	end;

OnCroixWin:
	set $@kvm62BG, 3;
	set $@kvm62BG_Victory, 2;

	stopnpctimer;
	sleep 1000;
	announce "- Red Team VENCEU! -",bc_all,0xff6347;
	sleep 3000;
	donpcevent "kvm62_BG::OnStop";
	end;

OnStop:
	if ($spectator_count10 > 0) {
       set $spectator_count10, $spectator_count10 - 1;
    }
	if ($spectator_count10 > 0) {
       set $spectator_count10, $spectator_count10 - 1;
    }
	// Warp Teams
	mapwarp "aldeg_cas02","izlude",128,105;
	donpcevent "kvm62_BG_Out::OnBegin";
	end;

OnReset:
	stopnpctimer;
	stopnpctimer "kvm62_BG_Out";
	set .Croix_Count, 0;
	set .Guillaume_Count, 0;
	set .Croix_Score, 0;
	set .Guillaume_Score, 0;
	set $@kvm62BG_Victory, 0;
	if( $@kvm62BG_id1 ) { bg_destroy $@kvm62BG_id1; set $@kvm62BG_id1, 0; }
	if( $@kvm62BG_id2 ) { bg_destroy $@kvm62BG_id2; set $@kvm62BG_id2, 0; }
	sleep 2000;
	mapwarp "aldeg_cas02","izlude",128,105;
	sleep 2000;
	maprespawnguildid "aldeg_cas02",0,3; // Just in case someone else
	bg_updatescore "aldeg_cas02",0,0;
	sleep 2000;
	set $@kvm62BG, 0;
	donpcevent "kvm62_BG::OnReadyCheck"; // Maybe a game is ready to start
	end;
}

-	script	kvm62_BG_Out	-1,{
	end;

OnBegin:
	initnpctimer;
	end;

OnTimer50000:
	mapannounce "aldeg_cas02","- The match will end in 10 seconds! -",1,0x696969;
	end;
OnTimer3000:
	donpcevent "kvm62_BG::OnReset";
	end;
}