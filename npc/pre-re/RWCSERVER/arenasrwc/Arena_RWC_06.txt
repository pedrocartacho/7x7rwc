// ==============================================================================
// Arenas 7vs7 modificada por rodrigo aka narutekz (2014), Fissuris (2017) e Moskaum Y (2024)
// ==============================================================================
quiz_02,131,351,5	script	Waiting Room::KvM36R_Guillaume	418,{
	end;
	
OnInit:
	while (1){
		sleep 2000;
		if (getwaitingroomstate (1) != $@jogadoresbluechat){
			delwaitingroom;
			waitingroom "Arena 6 - 2009rwc_02",$@jogadoresbluechat,"KvM36_BG::OnGuillaumeJoin",1;
			}
	}
	end;
	
OnEnterBG:
	set $@KvM36BG_id1, waitingroom2bg("izlude",128,105,"KvM36_BG::OnGuillaumeQuit","KvM36_BG::OnGuillaumeDie");
	end;
}

quiz_02,164,351,4	script	Waiting Room::KvM36R_Croix	414,{
	end;

OnInit:
	while (1){
		sleep 2000;
		if (getwaitingroomstate (1) != $@jogadoresredchat){
			delwaitingroom;
			waitingroom "Arena 6 - 2009rwc_02",$@jogadoresredchat,"KvM36_BG::OnCroixJoin",1;
			}
	}
	end;

OnEnterBG:
	set $@KvM36BG_id2, waitingroom2bg("izlude",128,105,"KvM36_BG::OnCroixQuit","KvM36_BG::OnCroixDie");
	end;
}

// Battleground Engine
// *********************************************************************

-	script	KvM36_BG	-1,{
	end;

OnGuillaumeJoin:
OnCroixJoin:
	donpcevent "KvM36_BG::OnReadyCheck";
	end;

OnGuillaumeQuit:
	set BG_Delay_Tick, gettimetick(2) + 2000;
OnGuillaumeDie:
	if( $@KvM36BG == 2 )
	{
		set .Guillaume_Count, .Guillaume_Count - 1;
		set .Croix_Score, .Croix_Score + 1;
		bg_updatescore "2009rwc_02",.Guillaume_Score,.Croix_Score;
		if( .Guillaume_Count < 1 ){
			donpcevent "KvM36_BG::OnCroixWin";
		}
	}
	end;

OnCroixQuit:
	set BG_Delay_Tick, gettimetick(2) + 2000;
OnCroixDie:
	if( $@KvM36BG == 2 )
	{
		set .Croix_Count, .Croix_Count - 1;
		set .Guillaume_Score, .Guillaume_Score + 1;
		bg_updatescore "2009rwc_02",.Guillaume_Score,.Croix_Score;
		if( .Croix_Count < 1 ){
			donpcevent "KvM36_BG::OnGuillaumeWin";
		}
	}
	end;

OnReadyCheck:
	if( $@KvM36BG )
		end;
	set .@Guillaume, getwaitingroomstate(0,"KvM36R_Guillaume");
	set .@Croix, getwaitingroomstate(0,"KvM36R_Croix");

	if((.@Guillaume < $@jogadoresblue) || (.@Croix < $@jogadoresred))
	{
		if( .@Guillaume > 3 && .@Croix > 3 && !agitcheck() && $@KvM_Flood < gettimetick(2) )
		{
			announce "Arena 6 - 2009rwc_02 | Blue Team: " + .@Guillaume + "/"+$@jogadoresblue+", Red Team: " + .@Croix + "/"+$@jogadoresred+"",0,0xFA8072;
			set $@KvM_Flood, gettimetick(2) + 15;
		}
		else
			mapannounce "quiz_02","Arena 6 - 2009rwc_02 | Blue Team: " + .@Guillaume + "/"+$@jogadoresblue+", Red Team: " + .@Croix + "/"+$@jogadoresred+"",1,0xFA8072;
		end;
	}
	
	if ((.@Guillaume == 0) || (.@Croix == 0)) {
		end;
	}
		
	set $@KvM36BG, 1; // Starting
	donpcevent "KvM36R_Croix::OnEnterBG";
	donpcevent "KvM36R_Guillaume::OnEnterBG";
	donpcevent "KvM36_BG::OnStart";
	end;

OnStart:
	announce "- Duas equipes entraram na [ Arena 8 - 2009rwc_02 ]. -", bc_yellow;
	set $@KvM36BG_Victory, 0;
	// Warp Teams
	bg_warp $@KvM36BG_id1,"2009rwc_02",9,50;
	bg_warp $@KvM36BG_id2,"2009rwc_02",90,50;

	if ($@guildsInArena > 0) {
        $@guildsInArena--;
    }
	if ($@guildsInArena > 0) {
        $@guildsInArena--;
    }

	setwall "2009rwc_02",17,51,4,4,0,"batc25wall_a";
	setwall "2009rwc_02",82,51,4,4,0,"batc25wall_d";

	sleep 100;
	// ScoreBoard
	set .Guillaume_Score, 0;
	set .Croix_Score, 0;
	initnpctimer;
	end;

OnTimer3000:
	mapannounce "2009rwc_02","- The match will begin in 15 seconds! -", bc_yellow;
	end;

OnTimer15000:
	mapannounce "2009rwc_02","- The match will begin in 5 seconds! -", bc_yellow;
	end;

OnTimer16000:
	mapannounce "2009rwc_02","- The match will begin in 4 seconds! -", bc_yellow;
	end;

OnTimer17000:
	mapannounce "2009rwc_02","- The match will begin in 3 seconds! -", bc_yellow;
	end;

OnTimer18000:
	mapannounce "2009rwc_02","- The match will begin in 2 seconds! -", bc_yellow;
	end;
	
OnTimer19000:
	mapannounce "2009rwc_02","- The match will begin in 1 second! -", bc_yellow;
	end;

OnTimer20000:
	// Team Members
	set .Guillaume_Count, bg_get_data($@KvM36BG_id1, 0);
	set .Croix_Count, bg_get_data($@KvM36BG_id2, 0);
	if( (.Guillaume_Count < $@jogadoresblue) || (.Croix_Count < $@jogadoresred) )
	{
		set $@KvM36BG_Victory, 3;
		set $@KvM36BG, 3;
		mapannounce "2009rwc_02","- There are not enough players to start the match. -", bc_yellow;
		stopnpctimer;
		sleep 2000;
		donpcevent "KvM36_BG::OnStop";
		end;
	}

	set $@KvM36BG, 2; // Playing
	delwall "batc25wall_a";
	delwall "batc25wall_d";
	delwall "batc25wall_ae01";
	delwall "batc25wall_ae02";
	end;

OnTimer25001:
	mapannounce "2009rwc_02","- The match started! Good luck to all. -", bc_yellow;
	killmonsterall "2009rwc_02";
	end;

OnTimer420000:
	mapannounce "2009rwc_02","- The match will end in 30 seconds! -", bc_yellow;
	end;

OnTimer450000:
	if( .Croix_Count > .Guillaume_Count )
		donpcevent "KvM36_BG::OnCroixWin";
	else if( .Croix_Count < .Guillaume_Count )
		donpcevent "KvM36_BG::OnGuillaumeWin";
	else
	{ // Draw Game
		set $@KvM36BG, 3;
		set $@KvM36BG_Victory, 3;

		stopnpctimer;
		sleep 2000;
		announce "- As duas equipes empataram! -", bc_yellow;
		donpcevent "KvM36_BG::OnStop";
	}
	end;

OnGuillaumeWin:
	set $@KvM36BG, 3;
	set $@KvM36BG_Victory, 1;

	stopnpctimer;
	sleep 1000;	
	announce "- Blue Team VENCEU! -",bc_all,0x00bfff;
	sleep 3000;
	donpcevent "KvM36_BG::OnStop";
	end;

OnCroixWin:
	set $@KvM36BG, 3;
	set $@KvM36BG_Victory, 2;

	stopnpctimer;
	sleep 1000;
	announce "- Red Team VENCEU! -",bc_all,0xff6347;
	sleep 3000;
	donpcevent "KvM36_BG::OnStop";
	end;

OnStop:
	if ($spectator_count6 > 0) {
       set $spectator_count6, $spectator_count6 - 1;
    }
	if ($spectator_count6 > 0) {
       set $spectator_count6, $spectator_count6 - 1;
    }
	// Warp Teams
	mapwarp "2009rwc_02","izlude",128,105;
	donpcevent "KvM36_BG_Out::OnBegin";
	end;

OnReset:
	stopnpctimer;
	stopnpctimer "KvM36_BG_Out";
	set .Croix_Count, 0;
	set .Guillaume_Count, 0;
	set .Croix_Score, 0;
	set .Guillaume_Score, 0;
	set $@KvM36BG_Victory, 0;
	if( $@KvM36BG_id1 ) { bg_destroy $@KvM36BG_id1; set $@KvM36BG_id1, 0; }
	if( $@KvM36BG_id2 ) { bg_destroy $@KvM36BG_id2; set $@KvM36BG_id2, 0; }
	sleep 2000;
	mapwarp "2009rwc_02","izlude",128,105;
	sleep 2000;
	maprespawnguildid "2009rwc_02",0,3; // Just in case someone else
	bg_updatescore "2009rwc_02",0,0;
	sleep 2000;
	set $@KvM36BG, 0;
	donpcevent "KvM36_BG::OnReadyCheck"; // Maybe a game is ready to start
	end;
}

-	script	KvM36_BG_Out	-1,{
	end;

OnBegin:
	initnpctimer;
	end;

OnTimer50000:
	mapannounce "2009rwc_02","- The match will end in 10 seconds! -",1,0x696969;
	end;
OnTimer3000:
	donpcevent "KvM36_BG::OnReset";
	end;
}